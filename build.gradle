plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.3.1'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "bridge" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

repositories {
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Use shade for dependencies you want to bundle
    shade("net.dv8tion:JDA:${project.jda_version}") {
        exclude group: 'club.minnced', module: 'opus-java'
    }
    shade 'com.squareup.okhttp3:okhttp:4.10.0'
    shade "club.minnced:discord-webhooks:0.8.4"
    shade 'org.xerial:sqlite-jdbc:3.51.1.0'
    shade "blue.endless:jankson:1.2.1"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

shadowJar {
    configurations = [project.configurations.shade]
    archiveClassifier = 'dev-all'

    // Exclude signature files
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    // Relocate packages (replace 'github.jcbsm.bridge' with your actual mod package)
    relocate 'net.dv8tion.jda', 'github.jcbsm.bridge.shadow.jda'
    relocate 'okhttp3', 'github.jcbsm.bridge.shadow.okhttp3'
    relocate 'okio', 'github.jcbsm.bridge.shadow.okio'
    relocate 'club.minnced', 'github.jcbsm.bridge.shadow.minnced'
    // DO NOT RELOCATE SQLITE - it has native libraries that break when relocated
    // relocate 'org.sqlite', 'github.jcbsm.bridge.shadow.sqlite'
    relocate 'blue.endless.jankson', 'github.jcbsm.bridge.shadow.jankson'
    relocate 'kotlin', 'github.jcbsm.bridge.shadow.kotlin'
    relocate 'com.fasterxml', 'github.jcbsm.bridge.shadow.fasterxml'
    relocate 'com.neovisionaries', 'github.jcbsm.bridge.shadow.neovisionaries'
    relocate 'gnu.trove', 'github.jcbsm.bridge.shadow.trove'
    relocate 'org.apache.commons', 'github.jcbsm.bridge.shadow.apache.commons'

    mergeServiceFiles()
}

remapJar {
    dependsOn shadowJar
    inputFile.set shadowJar.archiveFile.get()
    archiveClassifier = null
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
        // Add repositories to publish to here.
    }
}